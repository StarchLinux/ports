# $Id: PKGBUILD 154148 2012-03-23 17:34:14Z andyrtr $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xorg-setxkbmap
pkgver=1.3.0
pkgrel=1
pkgdesc='Set the keyboard using the X Keyboard Extension'
url='http://xorg.freedesktop.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('libxkbfile')
makedepends=('xorg-util-macros' 'libxcb')
source=("${url}/releases/individual/app/setxkbmap-${pkgver}.tar.bz2")
sha1sums=('d10885d6215b06789961c67557df46406f99c2b7')

groups=('xorg-apps' 'xorg')

build() {
	cd "${srcdir}/setxkbmap-${pkgver}"
	LIBS="-lxcb -lXau" \
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/setxkbmap-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
