# $Id: PKGBUILD 180197 2013-03-18 16:09:06Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-fbdev
pkgver=0.4.3
pkgrel=3
pkgdesc="X.org framebuffer video driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('pkg-config' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto' 'xproto' 'fontsproto' 'randrproto' 'renderproto' 'videoproto' 'kbproto' 'xextproto' 'inputproto' 'xineramaproto' 'pixman')
conflicts=('xorg-server<1.14.0')
groups=('xorg-drivers' 'xorg')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 git-fix.diff)
sha256sums=('ff7b037ad110040a4e2db5c84e9741125dbbaf3a08107db47760f3e11f9c4831'
            '4eb30cc4db5d1789429861d2d8f15238b21204d4311763ab7a1f4f32ff2b0321')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/git-fix.diff
  CPPFLAGS="-I/include/xorg -I/include/pixman-1" ./configure --prefix=/usr --disable-shared
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
