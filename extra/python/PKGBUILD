# $Id: PKGBUILD 186286 2013-05-23 20:40:14Z stephane $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=3.3.2
pkgrel=1
_pybasever=3.3
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org/"
depends=('bzip2' 'openssl' 'libffi' 'zlib')
optdepends=('tk: for tkinter' 'sqlite')
provides=('python3')
replaces=('python3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz
        python-3.3.2-CVE-2013-2099.patch)
sha1sums=('87009d0c156c6e1354dfec5c98c328cae93950ad'
          'b7a386b2e2f0811b344898500860ec31ba81ed4d')

prepare() {
  cd "${srcdir}/Python-${pkgver}"

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  #rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  patch -Np1 -i ../python-3.3.2-CVE-2013-2099.patch

  echo Modules/Setup.dist | situ sed '1i\
*static*
'
}

build() {
  cd "${srcdir}/Python-${pkgver}"

  ./configure --prefix=/usr \
              --disable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --with-system-ffi

  make
}

check() {
  cd "${srcdir}/Python-${pkgver}"
  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
     "${srcdir}/Python-${pkgver}/python" -m test.regrtest -x test_posixpath test_logging
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" install maninstall

  # Why are these not done by default...
  ln -sf python3               "${pkgdir}/usr/bin/python"
  ln -sf python3-config        "${pkgdir}/usr/bin/python-config"
  ln -sf idle3                 "${pkgdir}/usr/bin/idle"
  ln -sf pydoc3                "${pkgdir}/usr/bin/pydoc"
  ln -sf python${_pybasever}.1 "${pkgdir}/usr/share/man/man1/python3.1"

  # Clean-up reference to build directory
  sed -i "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
