# $Id: PKGBUILD 191317 2013-07-23 14:38:48Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng
pkgver=1.6.3
_apngver=1.6.3
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz)
#       http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz
md5sums=('9307f8ab04f4be3c1889ffb504f0fe5c')
#        '57007fc121baa2cdc48abcfb26300e78'

prepare() {
  cd $pkgname-$pkgver

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  #patch -Np 1 <../libpng-$_apngver-apng.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --disable-shared
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -D -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
