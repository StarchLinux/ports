# $Id: PKGBUILD 189873 2013-07-10 08:18:16Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.2.22
pkgrel=1
pkgdesc="Library for applications dealing with netlink sockets"
arch=(i686 x86_64)
url="http://www.infradead.org/~tgr/libnl/"
license=(GPL)
backup=(etc/libnl/classid etc/libnl/pktloc)
options=(!libtool)
makedepends=('flex' 'bison')
source=("$url/files/$pkgname-$pkgver.tar.gz")
sha256sums=('c7c5f267dfeae0c1a530bf96b71fb7c8dbbb07d54beef49b6712d8d6166f629b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-shared
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
