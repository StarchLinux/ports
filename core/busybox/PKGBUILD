# $Id: PKGBUILD 93409 2013-07-03 10:50:24Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.21.1
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="http://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
provides=('coreutils' 'grep' 'sed' 'findutils' 'diffutils' 'awk' 'util-linux' 'util-linux-ng' 'sysvinit' 'sysvinit-tools' 'which' 'tar' 'wget' 'psmisc' 'procps-ng' 'less' 'kmod')
source=($url/downloads/$pkgname-$pkgver.tar.bz2
	config)
install=busybox.install

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cp $srcdir/config .config
  echo include/libbb.h | situ sed '1i#include <sys/resource.h>'
  echo networking/libiproute/iplink.c | situ sed '\#<net/if_packet\.h>#d'
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # if you want to run menuconfig uncomment the following line:
#  make menuconfig ; return 1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make CONFIG_PREFIX="${pkgdir}" install
}

sha256sums=('cd5be0912ec856110ae12c76c3ec9cd5cba1df45b5a9da2b095b8284d1481303'
            '96a3a5b25a976f423edd920bf667f4cf08df7b38d07dc0c5753d6afe28944c7c')
