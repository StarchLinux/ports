# vim: set ts=2 sw=2 et:
# $Id: PKGBUILD 188784 2013-06-20 06:35:12Z allan $
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=pacman
pkgver=4.1.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
makedepends=('bash>=4.2.042-2' 'libarchive>=3.1.2')
depends=('pacman-mirrorlist' 'archlinux-keyring' 'curl')
checkdepends=('python2' 'fakechroot')
optdepends=('fakeroot: for makepkg usage as normal user')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman.conf.i686
        pacman.conf.x86_64
        makepkg.conf)
md5sums=('063c8b0ff6bdf903dc235445525627cd'
         'SKIP'
         '2db6c94709bb30cc614a176ecf8badb1'
         'de74a13618347f08ae4a9637f74471c4'
         '5f360e003e95f1a5cbd6e60d937dfd25')

build() {
  cd $srcdir/$pkgname-$pkgver

  CPPFLAGS="-DPATH_MAX=4096 $CPPFLAGS"
  LIBARCHIVE_CFLAGS=' ' LIBARCHIVE_LIBS='-larchive' \
  ac_cv_func_working_mktime=no \
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-shared --disable-doc \
    --with-scriptlet-shell=/usr/bin/bash
  make
  make -C contrib
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  make DESTDIR=$pkgdir -C contrib install

  # install Arch specific stuff
  install -dm755 $pkgdir/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/etc/pacman.conf
  
  mycarch="$CARCH"
  mychost="$CARCH-unknown-linux-gnu"
  myflags="-march=$CARCH"
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  echo "$pkgdir/etc/makepkg.conf" | situ sed \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
    
  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done
}
