# $Id: PKGBUILD 95881 2013-08-18 08:58:15Z ttoepper $
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Daniel Hommel <dhommel@gmail.com>

pkgname=mksh
_pkgver=R48b
pkgver=48.1
pkgrel=1
pkgdesc='The MirBSD Korn Shell - an enhanced version of the public domain ksh'
url='https://www.mirbsd.org/mksh.htm'
license=('custom')
arch=('i686' 'x86_64')
provides=('ksh')
install=mksh.install
source=("https://www.mirbsd.org/MirOS/dist/mir/mksh/$pkgname-$_pkgver.tgz"
        'https://www.mirbsd.org/TaC-mksh.txt')

check() {
  cd "$srcdir/$pkgname"
  ./test.sh
}

build() {
  cd "$srcdir/$pkgname"

  sh Build.sh -r
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m 755 mksh "$pkgdir/bin/mksh"
  install -D -m 644 mksh.1 "$pkgdir/doc/man/man1/mksh.1"
  install -D -m 644 dot.mkshrc "$pkgdir/etc/skel/.mkshrc"
  install -D -m 644 "$srcdir/TaC-mksh.txt" "$pkgdir/usr/share/licenses/mksh/TaC-mksh.txt"
  ln "$pkgdir/bin/mksh" "$pkgdir/bin/ksh"
}

sha256sums=('e67686187ba6dfbf99b7f6b5f841dfb5cd9f980504f8b631d7d3bfd98d345792'
            'SKIP')
