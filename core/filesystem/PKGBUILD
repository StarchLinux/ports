# $Id: PKGBUILD 187026 2013-06-03 11:15:07Z allan $
# Maintainer: Tom Gundersen <teg@jklm.no>

pkgname=filesystem
pkgver=2013.05
pkgrel=2
pkgdesc='Base filesystem'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.archlinux.org'
groups=('base')
install='filesystem.install'
makedepends=()
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/passwd'
        'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 'etc/motd'
        'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile' 'etc/issue')
source=('group' 'issue' 'securetty' 'host.conf'
        'passwd' 'shadow' 'fstab' 'crypttab' 'hosts' 'motd' 'os-release' 'resolv.conf'
        'shells' 'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first' 'archlinux.7.txt'
        'locale.sh')

package() {
	cd ${pkgdir}

	#
	# setup root filesystem
	#
	for d in boot dev etc home mnt usr var opt srv/http run; do
		install -d -m755 ${d}
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp

	# setup /etc
	install -d etc/{skel,profile.d}
	for f in fstab group host.conf hosts issue motd os-release passwd resolv.conf securetty shells profile; do
		install -m644 ${srcdir}/${f} etc/
	done
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done
	touch etc/arch-release
	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first usr/lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh

	# setup /var
	for d in cache/man local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	#
	# setup /usr hierarchy
	#
	for d in bin include lib share/misc src; do
		install -d -m755 usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 usr/share/man/man${d}
	done

	#
	# add lib symlinks
	#
	ln -s usr/lib ${pkgdir}/lib
	[[ ${CARCH} = "x86_64" ]] && (
		ln -s usr/lib ${pkgdir}/lib64
		ln -s lib ${pkgdir}/usr/lib64
	)

	#
	# add bin symlinks
	#
	ln -s usr/bin ${pkgdir}/bin
	ln -s usr/bin ${pkgdir}/sbin
	ln -s bin ${pkgdir}/usr/sbin

	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/${d}
	done
	ln -s ../man usr/local/share/man
}

sha256sums=('4767285e8b29bb09c96ed6b4c03e0f866a31ea60a45ce0a20ba8cc199ce7bcbd'
            'c774dbbcaea38ee1c1141d0daf82aa8177bfb26aca896d6f0c4ccdc902f6ac42'
            'f7b38167196943d8fb9d2e75358c7262d5fe1082b257c4c8caffe3b6de429cb5'
            'f21f35e7d3083aeee19beb48478130d119b2572922a4889df98326d551540eda'
            '84c8b6103553a88b43e83360446ea3da31e815b457eff233adfd636a304d399d'
            'bdf92c2a97996eb1b3be24090ca94100f16232290010a7395e44c5c92bd38027'
            '86dff00854a7f2280bb799484ccc39ad449a171448926880742ae64f41666a43'
            'ea8c67a34a0a8a41beabe3ceb5733c9e8b84b27c569c11ddbfd8395fce99d8dd'
            '13d1f59c1d0860bf43da996e2b9a6ab2744791614241ed9a1376b7e7e30de3a8'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            '29ef359e11c4d36c6aaa833b81bb2e4be8a5220a1ea426fab7d56420ba62f8ba'
            '74d2be3dd69600bcf0da751a1804a386bf4ad143b76d79c336acaa5c600e7991'
            '6cff886fe9e23096d5f67b7857a747ebaf10431be5a2afc40236de5ad13a654f'
            '2c4f081e1db3c855b8ab43514275a82263d50ac68b16c5eb5fcc2149c6b42aed'
            '0262cacc91cd309be0747e088d46a39f0b205392fbbafab96d767e48a3872ad0'
            '90fb123aa8c0176bc144322b585bb889181c437542ddbe546af43e11b95be6ab'
            '22f9ec6a8e42e7ea743f611cb34ee13d1f0b97e3d388f03c6fdca57886e0a163'
            '1d30a33a066e5788110d8d8ad95444d08b95f10d979f895a63b0a12bbfa7e243')
